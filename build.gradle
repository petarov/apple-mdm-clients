def gitExec = { List<String> args ->
    ByteArrayOutputStream out = new ByteArrayOutputStream()
    exec {
        commandLine = ['git'] + args
        standardOutput = out
        ignoreExitValue = true
    }
    String s = out.toString().trim()
    return s ? s : null
}

ext {
    gitCommitId = gitExec(['rev-parse', '--short', 'HEAD'])
    gitCommitTime = gitExec(['log', '-1', '--format=%cI', 'HEAD'])
}

println "--> Last commit id:   ${gitCommitId}"
println "--> Last commit time: ${gitCommitTime}"

allprojects {
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    group 'net.vexelon.mdm'
    ext.copyright = 'MIT License, Copyright (c) 2025 Petar Petrov'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        withSourcesJar()
        withJavadocJar()
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.provider { project.version },
                    "Implementation-Id": "${gitCommitId}",
                    "Implementation-LastModified": "${gitCommitTime}"
            )
        }
    }
}